apply plugin: 'idea'
apply plugin: 'kotlin'
apply plugin: 'java'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.sonarqube'


buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven { url "http://repo.spring.io/release" }
        maven { url "https://repo.spring.io/libs-milestone" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("io.spring.gradle:dependency-management-plugin:${dependencyPlugin}")
        classpath("org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:${sonarVersion}")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
    }
}

repositories {
    mavenCentral()
    jcenter()
    maven { url "http://repo.spring.io/release" }
    maven { url "https://repo.spring.io/libs-milestone" }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'kotlin'
    apply plugin: 'kotlin-spring'

    sourceCompatibility = compatibility
    targetCompatibility = compatibility

    tasks.withType(JavaCompile) {
        options.incremental = true
    }

    tasks.withType(Test) {
        testLogging {
            // set options for log level LIFECYCLE
            events "passed", "skipped", "failed", "standardOut"
            showExceptions true
            exceptionFormat "full"
            showCauses true
            showStackTraces true

            // set options for log level DEBUG and INFO
            debug {
                events "started", "passed", "skipped", "failed", "standardOut", "standardError"
                exceptionFormat "full"
            }
            info.events = debug.events
            info.exceptionFormat = debug.exceptionFormat

            afterSuite { desc, result ->
                if (!desc.parent) { // will match the outermost suite
                    def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                    def startItem = '|  ', endItem = '  |'
                    def repeatLength = startItem.length() + output.length() + endItem.length()
                    println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
                }
            }
        }
    }

    buildscript {
        repositories {
            mavenCentral()
        }
        dependencies {
            classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
        }
    }

    dependencyManagement {
        imports {
            mavenBom("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")
            mavenBom("org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}")
        }
    }

    repositories {
        mavenCentral()
        jcenter()
        maven { url "http://repo.spring.io/release" }
        maven { url "https://mymavenrepo.com/repo/tpcuo7p3QNPGqEXXYJZK/" } // comment it if you're using your version of utils lib
        maven { url "https://repo.spring.io/libs-milestone" }
    }

    dependencies {
        compile('org.springframework.boot:spring-boot-devtools')
        compile('org.springframework.cloud:spring-cloud-starter-hystrix')
        compile('org.springframework.boot:spring-boot-starter-actuator')
        compile('org.springframework.boot:spring-boot-starter-mail')
        compile('org.springframework.cloud:spring-cloud-starter-config')
        compile('org.jetbrains.kotlin:kotlin-stdlib-jre8:' + kotlinVersion)
        compile('org.jetbrains.kotlin:kotlin-reflect:' + kotlinVersion)
        compile('org.projectlombok:lombok:' + lombokVersion)
        compile('io.github.microutils:kotlin-logging:' + kotlinLoggerVersion)
        compile('pl.cyganki:e-arbiter-utils:' + version)   // comment it if you're using your version of utils lib
//        compile fileTree(dir: "${project.rootDir}/libs", include: ['*.jar'])    // PUSH COMMENTED!!! uncomment it if you're using your version of utils lib
        testCompile('org.springframework.boot:spring-boot-starter-test')
        testCompile('io.springfox:springfox-staticdocs:' + swaggerVersion)
    }
}

sonarqube {
    properties {
        property "sonar.projectName", "e-Arbiter"
        property "sonar.projectKey", "pl.cyganki:e-Arbiter"
        property "sonar.exclusions", "/**/*.c"
    }
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'

    version = version
    group = group
}

task markdownDoc(type: Delete) {

    def apiFile = new File("${project.rootDir}/API.md")
    apiFile.text = ""

    getModulePaths().eachWithIndex { String path, int i ->
        apiFile << replaceHeaderAndControllers(new File("$path/docs/paths.md"), i)
//        it should be cleaned but IJ run automatically this task...
//        delete fileTree("$path/docs/") { include '**/*.md' }
//        delete "$path/docs"
    }
}

configure(markdownDoc) {
    group = 'documentation'
    description = 'Create markdown documentation based on Swagger'
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlinVersion"
}

// FUNCTIONS

String replaceHeaderAndControllers(File file, int index) {

    try {
        file.text
                .replaceAll("## Resources", "## ${getHeader(index)}")
                .replaceAll("### \\w*-controller\n", "")
    } catch (FileNotFoundException ex) {
        // do nothing, this is workaround for gradle before checking if file exists (they will be later)
    }
}

String getHeader(int index) {
    def headers = [
            "Authentication Module",
            "Executor Module",
            "Tournament Module",
            "Tournament Results Module"
    ]

    headers[index]
}

String[] getModulePaths() {
    def modulePaths = [
            "./e-arbiter-auth",
            "./e-arbiter-executor",
            "./e-arbiter-tournament",
            "./e-arbiter-tournament-results"
    ]

    modulePaths
}